plugins {
    id 'application'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.google.protobuf' version '0.8.15'
    id 'idea' // use the idea plugin if using Jetbrains IntelliJ
}

group = 'com.ibm.aspera'
version = '1.1'

def grpcVersion = '1.37.0'
def protobufVersion = '3.12.0'
def protocVersion = protobufVersion

mainClassName = 'COSFileUploadExample'

repositories {
    mavenCentral()
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

sourceSets {
    main {
        proto {
            srcDir '../sdk/trsdk/noarch/connectors/java/src/main/protobuf'
            include '**/*.proto'
        }
    }
}

shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

dependencies {
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'org.json:json:20211205'
    implementation 'org.yaml:snakeyaml:1.30'
    compileOnly    'org.apache.tomcat:annotations-api:6.0.53'
}
