plugins {
    //id 'groovy'
    id 'application'
    //id 'java'
    // https://github.com/johnrengelman/shadow
    id 'com.github.johnrengelman.shadow' version '8.1.0'
    // https://plugins.gradle.org/plugin/com.google.protobuf
    id 'com.google.protobuf' version '0.8.19'
    //id 'idea' // use the idea plugin if using Jetbrains IntelliJ
}

group = 'com.ibm.aspera'
version = '0.1'

def grpcVersion = '1.43.2'

application {
  mainClass = 'COSFileUploadExample'
}

jar {
  manifest {
    attributes 'Main-Class': application.mainClass
  }
}

repositories {
    mavenCentral()
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.19.2" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

sourceSets {
    main {
        proto {
            // shall be: dirname $(CONFIG_TRSDK_PROTO)
            srcDir "../sdk/trsdk/noarch/connectors/grpc"
            include 'transfer.proto'
            exclude '*/**/*.proto'
        }
    }
}

// build jar with -all ext withh all dependincies in it
shadowJar {
    project.configurations.implementation.canBeResolved = true
    configurations = [project.configurations.implementation]
}

dependencies {
    compileOnly    'org.apache.tomcat:annotations-api:6.0.53'
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'org.json:json:20211205'
    implementation 'org.yaml:snakeyaml:1.30'
}
